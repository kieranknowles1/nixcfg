{
  lib,
  config,
  pkgs,
  ...
}: let
  mutexOptionsMsg = ''
    Exactly one of the following options must be specified:
    - `root`
    - `proxyPort`
    - `proxySocket`
  '';
in {
  imports = [
    ./actual.nix
    ./adguard.nix
    ./copyparty.nix
    ./docs.nix
    ./forgejo.nix
    ./homepage
    ./immich.nix
    ./minecraft.nix
    ./paperless.nix
    ./ports.nix
    ./postgresql.nix
    ./search.nix
    ./authelia
    ./trilium.nix
  ];

  options.custom.server = let
    inherit (lib) mkOption mkEnableOption types;

    mkHostOpt = type: example: description:
      mkOption {
        inherit example;
        type = types.nullOr type;
        default = null;
        description = "${description}\n${mutexOptionsMsg}";
      };

    vhostOpts = {
      root =
        mkHostOpt types.path "/path/to/html"
        "Absolute path to the root directory to be served.";
      proxyPort =
        mkHostOpt types.port 8080
        "The port to proxy connections to.";
      proxySocket =
        mkHostOpt types.path "/path/to/socket.sock"
        "Absolute path to socket to proxy connections to.";

      requireAuth = mkOption {
        type = types.bool;
        default = false;
        description = "Require basic authentication for this vhost";
      };

      webSockets = mkEnableOption "websockets";

      cache.enable = mkEnableOption "add cache headers to this vhost";
      cache.expires = mkOption {
        type = types.str;
        default = "12w";
        description = ''
          The `cache-control max-age` header value as an Nginx
          [time string](https://nginx.org/en/docs/syntax.html)
        '';
      };
    };

    subdomainType = types.submodule {
      options = vhostOpts;
    };
  in {
    enable = mkEnableOption "server hosting";

    hostname = mkOption {
      type = types.str;
      example = "example.com";
      description = "The domain name of the server";
    };

    basicAuthSecret = mkOption {
      type = types.str;
      example = "sops/basic-auth-secret";
      default = "nginx/basic-auth";
      description = "SOPS secret path to the basic auth secret file";
    };

    ssl = {
      publicKeyFile = mkOption {
        type = types.path;
        example = "/path/to/public.pem";
        description = ''
          Path to a public certificate file generated by
          [CloudFlare's origin CA](https://developers.cloudflare.com/ssl/origin-configuration/origin-ca/).
        '';
      };
      privateKeySecret = mkOption {
        type = types.str;
        example = "ssl/private-key";
        description = ''
          SOPS secret path to the private key file generated by
          [CloudFlare's origin CA](https://developers.cloudflare.com/ssl/origin-configuration/origin-ca/).
        '';
      };
    };

    root = vhostOpts;
    localRoot = vhostOpts;
    subdomains = mkOption {
      type = types.attrsOf subdomainType;
      default = {};
      description = "Subdomains to serve on the server";
    };

    data = {
      baseDirectory = mkOption {
        type = types.path;
        example = "/path/to/server/data";
        description = ''
          Base directory for storing server data. Should be backed up regularly.

          Services will use subdirectories within this unless configured otherwise.
        '';
      };
    };

    clearCacheKey = mkOption {
      type = types.str;
      default = "cloudflare/clearcache-key";
      description = ''
        SOPS secret path to a Cloudflare API key with the `Cache Purge` permission.
        Permission should be restricted to the zone ass
      '';
    };

    zoneIdSecret = mkOption {
      type = types.str;
      default = "cloudflare/zoneid";
      description = ''
        SOPS secret path to a Cloudflare zone ID.
      '';
    };
  };

  config = let
    cfg = config.custom.server;

    mkVhost = subdomain: ssl: {
      locations."/" = {
        inherit (subdomain) root;
        proxyPass =
          if subdomain.proxyPort != null
          then "http://localhost:${toString subdomain.proxyPort}"
          else if subdomain.proxySocket != null
          then "http://unix:${toString subdomain.proxySocket}"
          else null;

        proxyWebsockets = subdomain.webSockets;

        basicAuthFile = lib.mkIf subdomain.requireAuth config.sops.secrets.nginx-basic-auth.path;
      };

      forceSSL = ssl; # Enable HTTPS and redirect HTTP to it

      # We're not using ACME as it's incompatible with Cloudflare proxies
      # I'd rather not turn that off to reduce load from scraping, public pages
      # are static and should have high cache hit rates
      #
      # Instead, use a Cloudflare origin CA, which is only recognised by
      # Cloudflare's proxy to keep traffic secure
      sslCertificate = cfg.ssl.publicKeyFile;
      sslCertificateKey = config.sops.secrets.ssl-private-key.path;

      extraConfig = ''
        ${lib.optionalString subdomain.cache.enable "expires ${subdomain.cache.expires};"}
      '';
    };

    isSet = val: val != null;

    mkSubHosts = ssl: tld:
      lib.attrsets.mapAttrs' (name: subdomain: {
        name = "${name}.${tld}";
        value = mkVhost subdomain ssl;
      })
      cfg.subdomains;
  in
    lib.mkIf cfg.enable {
      assertions =
        lib.attrsets.mapAttrsToList (name: subdomain: {
          assertion =
            builtins.length (builtins.filter isSet [
              subdomain.root
              subdomain.proxyPort
              subdomain.proxySocket
            ])
            == 1;
          message = "config.custom.server.subdomains.${name}: \n${mutexOptionsMsg}";
        })
        cfg.subdomains;

      sops.secrets = let
        mkSecret = owner: key: {
          inherit owner key;
        };
      in {
        ssl-private-key = mkSecret config.services.nginx.user cfg.ssl.privateKeySecret;
        nginx-basic-auth = mkSecret config.services.nginx.user cfg.basicAuthSecret;

        cf-zone-id = mkSecret "root" cfg.zoneIdSecret;
        cf-clearcache-token = mkSecret "root" cfg.clearCacheKey;
      };

      services.nginx = {
        enable = true;
        # Compress responses using sensible defaults
        recommendedGzipSettings = true;
        # Apply sensible defaults to reverse proxies
        recommendedProxySettings = true;

        # Allow larger uploads, needed for things like Immich
        clientMaxBodySize = "50000m";

        # *.local can't use aliases since we don't have a certificate for .local,
        # meaning clients will complain about invalid certificates
        virtualHosts = lib.mkMerge [
          (mkSubHosts true cfg.hostname)
          (mkSubHosts false "${config.networking.hostName}.local")
          {
            "${cfg.hostname}" = mkVhost cfg.root true;
            "${config.networking.hostName}.local" = mkVhost cfg.localRoot false;

            # 404 for any unknown subdomains
            "_default" = {
              default = true;
              locations."/".return = 404;

              forceSSL = true; # Enable HTTPS and redirect HTTP to it
              sslCertificate = cfg.ssl.publicKeyFile;
              sslCertificateKey = config.sops.secrets.ssl-private-key.path;
            };
          }
        ];
      };

      networking.firewall.allowedTCPPorts = with cfg.ports.tcp; [http https];

      environment.systemPackages = lib.singleton (pkgs.writeShellScriptBin "clear-cloudflare-cache" ''
        set -euo pipefail

        ZONE_ID=$(cat ${config.sops.secrets.cf-zone-id.path})
        CLEAR_CACHE_TOKEN=$(cat ${config.sops.secrets.cf-clearcache-token.path})

        curl "https://api.cloudflare.com/client/v4/zones/$ZONE_ID/purge_cache" \
          --header 'Content-Type: application/json' \
          --header "Authorization: Bearer $CLEAR_CACHE_TOKEN" \
          --data '{"purge_everything": true}'
      '');
    };
}
