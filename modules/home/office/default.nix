{
  pkgs,
  config,
  hostConfig,
  lib,
  inputs,
  ...
}: {
  options.custom.office = let
    inherit (lib) mkOption mkEnableOption types;
  in {
    enable = mkEnableOption "office suite";

    paperless = {
      url = mkOption {
        type = types.str;
        example = "https://paperless.example.com";
        description = "URL of the paperless instance";
      };
      apiKeySecret = mkOption {
        type = types.str;
        default = "paperless/apikey";
        description = "SOPS secret for a Paperless API key";
      };
    };
  };

  config = let
    cfg = config.custom.office;
  in
    lib.mkIf cfg.enable {
      # Add templates for office applications, if we are installing a suite
      # These were generated by running the relevant applications and saving their blank files
      custom.desktop.templates = {
        "Document.odt" = ./templates/Document.odt;
        "Presentation.odp" = ./templates/Presentation.odp;
        "Spreadsheet.ods" = ./templates/Spreadsheet.ods;
      };

      custom.librewolf.extraExtensions = [
        inputs.firefox-addons.packages.${pkgs.system}.zotero-connector
      ];

      sops.secrets."paperless/apikey".key = "paperless/apikey";

      home.packages = with pkgs; [
        libreoffice
        # Needed for spell check
        hunspell
        # It's "Specialise" not "Specialize", we're British
        hunspellDicts.en_GB-ise

        # Research
        zotero

        (pkgs.flake.scandoc.override {
          paperlessUrl = cfg.paperless.url;
          paperlessTokenFile = config.sops.secrets."paperless/apikey".path;
          scanner = hostConfig.custom.printing.scanner.device;
        })
      ];
    };
}
